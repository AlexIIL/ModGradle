apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'

group 'xyz.openmodloader.gradle'

version '0.0.5'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    deployerJars
    shade
    compile.extendsFrom shade
}

dependencies {
    compile gradleApi()
    shade 'org.zeroturnaround:zt-zip:1.9'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:slf4j-simple:1.7.21'
    shade 'com.google.code.gson:gson:2.6.2'
    shade 'com.googlecode.java-diff-utils:diffutils:1.3.0'
    shade 'com.cloudbees:diff4j:1.2'
    shade 'commons-io:commons-io:1.4'
    shade 'com.google.guava:guava-jdk5:17.0'

}

sourceSets {
    main.compileClasspath += configurations.shade;
    main.runtimeClasspath += configurations.shade;
    test.compileClasspath += configurations.shade;
    test.runtimeClasspath += configurations.shade;
}

jar {

    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty('mavenPass'))
            {
                repository(url: "http://mavenupload.modmuss50.me/") {
                    authentication(userName: "buildslave", password: project.getProperty('mavenPass'))
                }
            }
            else
            {
                repository(url: "file:///var/www/maven/")
            }
            pom {
                groupId = "ModGradle"
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    developers {
                        developer {
                            id 'modmuss50'
                            name 'modmuss50'
                            roles { role 'developer' }
                        }
                        developer {
                            id 'lclc98'
                            name 'lclc98'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}